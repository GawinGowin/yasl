#!/usr/local/bin/yasl
{
  0                // when set 1, show statistics
  0:=:9 0:!:0
  9:? (
    "=============\n" 0:print
    0:#:0 "stack 0: " 0:print
    0:print "\n" 0:print
    1:#:0 "stack 1: " 0:print
    0:print "\n" 0:print
    2:#:0 "stack 2: " 0:print
    0:print "\n" 0:print
  )
} statistics

{
  "Error: invalid param\n" print
  statistics
  1 exit
} msg_invalid

{
  "Error: value is too large\n" print
  statistics
  1 exit
} msg_large

{[#]} strlen

{
  2:(1):2                  // ret
  0:=:1 !
  1:strlen:1               // n = strlen(str); n = i;
  1:%@ (                   // while (i > 0)
    1:(1 -):1              // i = i - 1;
    %]#[:1 =:1             // get str[i], dup str[i]

    1:("0"):1
    1:(2 ^):1 >:2          // "0" > "str[i]" = tmp1
    1:("9"):1
    1:(2 ^):1 <:2          // "9" < "str[i]" = tmp1
    2:||:1 ? (             // tmp1 || tmp1
      1:(=:1 -:1)
      2:(0):2 2:&:2
    )
  )
  1:(!!)
  2:=:0 2:!
} isdigit

{
  = 0 != ? (
    0:=:1
    0:(1 -):0
    0:fact
    1:=:0 1:!
    0:(2 ^ *):0
  ) : (
    ! 0:(1):0              // 0! = 1
  )
} fact

{
  2 != ? (
    # #! msg_invalid
  )
  !                        // drop argv[0]
  = isdigit 0 == ? (
    # #! msg_invalid
  )
  = 20 > ? (
    # #! msg_large
  )
  fact
  print "\n" print
} main

main
statistics

// 12!      = 479001600
// INT_MAX  = 2147483647
// 13!      = 6227020800
// 15!      = 1307674368000
// 20!      = 2432902008176640000
// 2^63 - 1 = 9223372036854775807
// 21!      = 51090942171709440000
